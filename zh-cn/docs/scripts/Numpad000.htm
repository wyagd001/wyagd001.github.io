<!DOCTYPE HTML>
<html>
<head>
<title>小键盘上的 000 键 - 脚本示例 | AutoHotkey</title>
<meta name="description" content="This script allows the special key 000, which appears on certain keyboards, to perform any action, such as sending the equals key." />
<meta http-equiv="content-type" content="text/html; charset=utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>小键盘上的 000 键</h1>

<p>此示例脚本把某些小键盘上特殊的 <kbd>000</kbd> 键设置为等号键. 您可以根据您的意愿替换 <code>Send, =</code> 这行来更改动作.
</p>
<p><a href="Numpad000.ahk">下载此脚本</a> &nbsp;| &nbsp;<a href="index.htm">其他示例脚本</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">主页</a></p>

<pre class="NoIndent">#MaxThreadsPerHotkey 5  <em>; Allow multiple threads for this hotkey.</em>
$Numpad0::
#MaxThreadsPerHotkey 1
<em>; Above: Use the $ to force the hook to be used, which prevents an
; infinite loop since this subroutine itself sends Numpad0, which
; would otherwise result in a recursive call to itself.</em>
SetBatchLines, 100 <em>; Make it run a little faster in this case.</em>
DelayBetweenKeys = 30 <em>; Adjust this value if it doesn't work.</em>
if A_PriorHotkey = %A_ThisHotkey%
{
    if A_TimeSincePriorHotkey &lt; %DelayBetweenKeys%
    {
        if Numpad0Count =
            Numpad0Count = 2 <em>; i.e. This one plus the prior one.</em>
        else if Numpad0Count = 0
            Numpad0Count = 2
        else
        {
            <em>; Since we're here, Numpad0Count must be 2 as set by</em>
            <em>; prior calls, which means this is the third time the</em>
            <em>; the key has been pressed. Thus, the hotkey sequence</em>
            <em>; should fire:</em>
            Numpad0Count = 0
            Send, = <em>; ******* This is the action for the 000 key</em>
        }
        <em>; In all the above cases, we return without further action:</em>
        CalledReentrantly = y
        return
    }
}
<em>; Otherwise, this Numpad0 event is either the first in the series
; or it happened too long after the first one (e.g. perhaps the
; user is holding down the Numpad0 key to auto-repeat it, which
; we want to allow).  Therefore, after a short delay -- during
; which another Numpad0 hotkey event may re-entrantly call this
; subroutine -- we'll send the key on through if no reentrant
; calls occurred:</em>
Numpad0Count = 0
CalledReentrantly = n
<em>; During this sleep, this subroutine may be reentrantly called
; (i.e. a simultaneous "thread" which runs in parallel to the
; call we're in now):</em>
Sleep, %DelayBetweenKeys%
if CalledReentrantly = y <em>; Another "thread" changed the value.</em>
{
    <em>; Since it was called reentrantly, this key event was the first in</em>
    <em>; the sequence so should be suppressed (hidden from the system):</em>
    CalledReentrantly = n
    return
}
<em>; Otherwise it's not part of the sequence so we send it through normally.
; In other words, the *real* Numpad0 key has been pressed, so we want it
; to have its normal effect:</em>
Send, {Numpad0}
return
</pre>
</body>
</html>